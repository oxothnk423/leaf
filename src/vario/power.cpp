// Includes
#include "power.h"

#include "diagnostics/diagnostic_network/diagnostic_network.h"
#include "hardware/Leaf_I2C.h"
#include "hardware/Leaf_SPI.h"
#include "hardware/aht20.h"
#include "hardware/buttons.h"
#include "hardware/configuration.h"
#include "hardware/icm_20948.h"
#include "hardware/io_pins.h"
#include "hardware/lc86g.h"
#include "hardware/ms5611.h"
#include "instruments/baro.h"
#include "instruments/gps.h"
#include "instruments/imu.h"
#include "logging/buslog.h"
#include "logging/log.h"
#include "power.h"
#include "storage/sd_card.h"
#include "ui/audio/sound_effects.h"
#include "ui/audio/speaker.h"
#include "ui/display/display.h"
#include "ui/display/display_fields.h"
#include "ui/display/pages/dialogs/page_alert_autoOff.h"
#include "ui/input/buttons.h"
#include "ui/settings/settings.h"
#include "utils/magic_enum.h"

// Alert page to warn if auto-off is about to occur
PageAlertAutoOff pageAlertAutoOff;

Power power;  // struct for battery-state and on-state variables

// Pinout for Leaf V3.2.0+
#define POWER_LATCH 48
#define BATT_SENSE 1  // INPUT ADC

// Battery Threshold values
#define BATT_FULL_MV 4080   // mV full battery on which to base % full (100%)
#define BATT_EMPTY_MV 3250  // mV empty battery on which to base % full (0%)
#define BATT_SHUTDOWN_MV 3200
// mV at which to shutdown the system to prevent battery over-discharge
// Note: the battery apparently also has over discharge protection, but we don't fully trust it,
// plus we want to shutdown while we have power to save logs etc

// Auto-Power-Off Threshold values
#define AUTO_OFF_MAX_SPEED 3   // mph max -- must be below this speed for timer to auto-stop
#define AUTO_OFF_MAX_ACCEL 10  // Max accelerometer signal
#define AUTO_OFF_MAX_ALT 400   // cm altitude change for timer auto-stop

const char* nameOf(PowerInputLevel level) {
  switch (level) {
    case PowerInputLevel::Standby:
      return "Standby";
    case PowerInputLevel::i100mA:
      return "100mA";
    case PowerInputLevel::i500mA:
      return "500mA";
    case PowerInputLevel::Max:
      return "Max";
  }
  return "Unknown";
}

void Power::bootUp() {
  // Init Peripheral Busses
  wire_init();
  Serial.println(" - Finished I2C Wire");
  spi_init();
  Serial.println(" - Finished SPI");

  // initialize IO expander (needed for speaker in v3.2.5+ and power supply in v3.2.6+)
#ifdef HAS_IO_EXPANDER
  ioexInit();  // initialize IO Expander
  Serial.println(" - Finished IO Expander");
#endif

  initPowerSystem();  // configure power supply

  // initialize Buttons and check if holding the center button is what turned us on
  Button button = buttons.init();

  // go to "ON" state if button (user input) or BOOT_TO_ON flag from firmware update
  if (button == Button::CENTER || settings.boot_toOnState) {
    settings.boot_toOnState = false;
    settings.save();

    info_.onState = PowerState::On;

    display.showOnSplash();  // show the splash screen if user turned us on
    display.setPage((MainPage)settings.startPage);

    maybeStartBusLog();
  } else {
    // if not center button, then USB power turned us on, go into charge mode
    info_.onState = PowerState::OffUSB;
    display.setPage(MainPage::Charging);
  }

  // init peripherals (even if we're not turning on and just going into
  // charge mode, we still need to initialize devices so we can put some
  // of them back to sleep)
  initPeripherals();
}

void Power::initPowerSystem() {
  Serial.print("power_init: ");
  Serial.println(nameOf(info_.onState));

  // Set output / input pins to control battery charge and power supply
  pinMode(BATT_SENSE, INPUT);
  pinMode(POWER_LATCH, OUTPUT);
  if (!POWER_CHARGE_I1_IOEX) pinMode(POWER_CHARGE_I1, OUTPUT);
  if (!POWER_CHARGE_I2_IOEX) pinMode(POWER_CHARGE_I2, OUTPUT);
  if (!POWER_CHARGE_GOOD_IOEX) pinMode(POWER_CHARGE_GOOD, INPUT_PULLUP);
  // POWER_GOOD is only available on v3.2.6+ on IOexpander, so not set here

  // set default current limit for charger input
  setInputCurrent(PowerInputLevel::i500mA);

#ifdef LED_PIN
  pinMode(LED_PIN, OUTPUT);  // LED power status indicator
#endif

#ifdef ISET
  pinMode(ISET, INPUT);  // pin to read actual charge current (if used)
#endif
}

void Power::initPeripherals() {
  Serial.print("init_peripherals: ");
  Serial.println(nameOf(info_.onState));

  if (info_.onState == PowerState::On) {
    latchOn();
    speaker.playSound(fx::enter);
    // loop until sound is done playing
    while (speaker.update()) {
      delay(10);
    }
  } else {
    latchOff();  // turn off 3.3V regulator (if we're plugged into USB, we'll stay on)
  }

  // then initialize the rest of the devices
  sdcard.init();
  Serial.println(" - Finished SDcard");
  lc86g.init();
  gps.init();
  Serial.println(" - Finished GPS");
  wire_init();
  Serial.println(" - Finished I2C Wire");
  display.init();
  Serial.println(" - Finished display");
  ms5611.init();
  Serial.println(" - Finished Baro");
  ICM20948::getInstance().init();
  Serial.println(" - Finished IMU");

  // then put devices to sleep if we're in PowerState::OffUSB
  // (plugged into USB but vario not actively turned on)
  if (info_.onState == PowerState::OffUSB) {
    sleepPeripherals();
  }
  Serial.println(" - DONE");
}

void Power::sleepPeripherals() {
  Serial.print("sleep_peripherals: ");
  Serial.println(nameOf(info_.onState));
  // TODO: all the rest of the peripherals not needed while charging
  Serial.println(" - Sleeping GPS");
  lc86g.sleep();
  Serial.println(" - Sleeping baro");
  baro.sleep();
  Serial.println(" - Sleeping speaker");
  speaker.mute();
  Serial.println("Shut down speaker");
  Serial.println(" - DONE");
}

void Power::wakePeripherals() {
  Serial.println("wake_peripherals: ");
  sdcard.mount();  // re-initialize SD card in case card state was changed while in charging/USB
                   // mode
  Serial.println(" - waking GPS");
  lc86g.wake();
  Serial.println(" - waking baro and IMU");
  baro.wake();
  imu.wake();
  Serial.println(" - waking speaker");
  speaker.unMute();
  Serial.println(" - DONE");
}

void Power::switchToOnState() {
  latchOn();
  Serial.println("switch_to_on_state");
  info_.onState = PowerState::On;
  wakePeripherals();
  if (diagnostic_network.shouldResetWhenSwitchingOn()) {
    diagnostic_network.reset("switch_to_on_state");
  }
  maybeStartBusLog();
}

void Power::maybeStartBusLog() {
  if (settings.dev_startLogAtBoot) {
    Serial.println("Starting bus log at startup");
    if (busLog.startLog()) {
      Serial.println("Started bus log at startup");
    } else {
      Serial.println("Failed to start bus log at startup");
      speaker.playSound(fx::bad);
    }
  }
}
void Power::shutdown() { shutdown(false); }

void Power::shutdown(bool deadBattery) {
  Serial.println("power_shutdown");

  switch (display.getPage()) {
    case MainPage::Debug:
    case MainPage::Simple:
    case MainPage::Thermal:
    case MainPage::ThermalAdv:
    case MainPage::Nav:
    case MainPage::Games:
      settings.startPage = (uint8_t)display.getPage();
      break;
    default:
      break;
  }

  // save logs and system data
  if (flightTimer_isRunning()) {
    flightTimer_stop(false);
  }

  // Show user we're shutting down
  display.clearPage();
  if (deadBattery) {
    display_batteryDead_splash();
  } else {
    display_off_splash();
  }

  baro.sleep();  // stop getting climbrate updates so we don't hear vario beeps while shutting down

  // play shutdown sound
  speaker.playSound(fx::exit);

  // loop until sound is done playing
  while (speaker.update()) {
    delay(10);
  }

  // save any changed settings this session
  settings.save();

  // wait another 2.5 seconds before shutting down to give user
  // a chance to see the shutdown screen
  delay(2500);

  // finally, turn off devices
  sleepPeripherals();
  display.clear();
  delay(100);
  latchOff();  // turn off 3.3V regulator (if we're plugged into USB, we'll stay on)
  delay(100);

  // go to PowerState::OffUSB, in case device was shut down while
  // plugged into USB, then we can show necessary charging updates etc
  info_.onState = PowerState::OffUSB;
  display.setPage(MainPage::Charging);
}

void Power::latchOn() { digitalWrite(POWER_LATCH, HIGH); }

void Power::latchOff() { digitalWrite(POWER_LATCH, LOW); }

void Power::update() {
  // first check for USB power and turn on LED if so
  // (only for v3.2.6+ with controllable LED and PowerGood input)
#ifdef LED_PIN
  info_.USBinput = !ioexDigitalRead(POWER_GOOD_IOEX, POWER_GOOD);
  if (info_.USBinput) {
    digitalWrite(LED_PIN, LOW);  // turn on LED if charging
  } else {
    digitalWrite(LED_PIN, HIGH);  // turn off LED if not charging
  }
#endif

  // update battery state
  readBatteryState();

  // check if we should shut down due to low battery..
  // TODO: should we only do this if we're NOT charging?
  //       Probably not, since shutting down allows more current for charging
  if (info_.batteryMV <= BATT_SHUTDOWN_MV) {
#ifndef DISABLE_BATTERY_SHUTDOWN
    shutdown(true);
#endif

    // ..or if we should shutdown due to inactivity
    // (only check if user setting is on and flight timer is stopped)
  } else if (settings.system_autoOff && !flightTimer_isRunning()) {
    // check if inactivity conditions are met
    if (autoOff()) {
      shutdown();  // shutdown!
    }
  }
}

bool showingAlertAutoOff = false;

void Power::resetAutoOffCounter() {
  autoOffCounter_ = 0;
  if (showingAlertAutoOff) {
    buttons.consumeButton();
    pageAlertAutoOff.closeAlert();  // close the alert if it's open, since we're shutting down now
    showingAlertAutoOff = false;    // reset alert page flag so it can show again after this reset
  }
}

uint16_t Power::getAutoOffSecondsRemaining() {
  return (settings.system_autoOff * 60 - autoOffCounter_);
}

bool Power::autoOff() {
  bool autoShutOff = false;  // start with assuming we're not going to turn off

  autoOffCounter_++;
  if (autoOffCounter_ >= settings.system_autoOff * 60) {  // convert minutes to seconds
    autoShutOff = true;
  } else if (autoOffCounter_ >= settings.system_autoOff * 60 - 10) {
    // start playing warning sounds 10 seconds before it auto-turns off
    speaker.playSound(fx::decrease);

    // and pop up the alert if we have 10 seconds left
    if (autoOffCounter_ == settings.system_autoOff * 60 - 10 && !showingAlertAutoOff) {
      pageAlertAutoOff.show();  // show alert page with countdown until auto-off
      showingAlertAutoOff = true;
    }
  }
  if (autoShutOff) {
    pageAlertAutoOff.closeAlert();  // close the alert if it's open, since we're shutting down now
    showingAlertAutoOff =
        false;  // reset alert page flag for next time we turn on and start counting
  }
  return autoShutOff;
}

void Power::readBatteryState() {
  // Update Charge State
  info_.charging = !ioexDigitalRead(POWER_CHARGE_GOOD_IOEX, POWER_CHARGE_GOOD);
  // logic low is charging, logic high is not

  info_.chargeCurrentMA = 0;
#ifdef ISET
  // V_ISET = (I_CHARGE / 400) * R_ISET note: R_ISET is 1100 Ohms
  info_.chargeCurrentMA = analogReadMilliVolts(ISET) * 400 / 1100;
#endif

  // Test internal calibration of ADC:
  info_.batteryMV = analogReadMilliVolts(BATT_SENSE) * 69 / 41;

  if (info_.batteryMV < BATT_EMPTY_MV) {
    info_.batteryPercent = 0;
  } else if (info_.batteryMV > BATT_FULL_MV) {
    info_.batteryPercent = 100;
  } else {
    info_.batteryPercent = 100 * (info_.batteryMV - BATT_EMPTY_MV) / (BATT_FULL_MV - BATT_EMPTY_MV);
  }

  // use a 2% hysteresis on battery% to avoid rapid fluctuations as ADC values change
  if (info_.charging) {  // don't let % go down (within 2%) when charging
    if (info_.batteryPercent < batteryPercentLast_ &&
        info_.batteryPercent >= batteryPercentLast_ - 2) {
      info_.batteryPercent = batteryPercentLast_;
    }
  } else {  // don't let % go up (within 2%) when discharging
    if (info_.batteryPercent > batteryPercentLast_ &&
        info_.batteryPercent <= batteryPercentLast_ + 2) {
      info_.batteryPercent = batteryPercentLast_;
    }
  }
  batteryPercentLast_ = info_.batteryPercent;
}

void Power::increaseInputCurrent() { setInputCurrent(++info_.inputCurrent); }

void Power::decreaseInputCurrent() { setInputCurrent(--info_.inputCurrent); }

// Note: the Battery Charger Chip has controllable input current (which is then used for both batt
// charging AND system load).  The battery will be charged with whatever current is remaining
// after system load.
void Power::setInputCurrent(PowerInputLevel current) {
  info_.inputCurrent = current;
  switch (current) {
    case PowerInputLevel::i100mA:
      ioexDigitalWrite(POWER_CHARGE_I1_IOEX, POWER_CHARGE_I1, LOW);
      ioexDigitalWrite(POWER_CHARGE_I2_IOEX, POWER_CHARGE_I2, LOW);
      break;
    default:
    case PowerInputLevel::i500mA:
      // set I2 to low first, so we don't accidentally have both set to High (if coming from iMax)
      ioexDigitalWrite(POWER_CHARGE_I2_IOEX, POWER_CHARGE_I2, LOW);
      ioexDigitalWrite(POWER_CHARGE_I1_IOEX, POWER_CHARGE_I1, HIGH);
      break;
    case PowerInputLevel::Max:
      // Approx 1.348A max input current (set by ILIM pin resistor value).
      // In this case, battery charging will then be limited by the max fast-charge limit
      // set by the ISET pin resistor value (approximately 810mA to the battery).
      ioexDigitalWrite(POWER_CHARGE_I1_IOEX, POWER_CHARGE_I1, LOW);
      ioexDigitalWrite(POWER_CHARGE_I2_IOEX, POWER_CHARGE_I2, HIGH);
      break;
    case PowerInputLevel::Standby:
      // USB Suspend mode - turns off USB input power (no charging or supplemental
      // power, but battery can still power the system)
      ioexDigitalWrite(POWER_CHARGE_I1_IOEX, POWER_CHARGE_I1, HIGH);
      ioexDigitalWrite(POWER_CHARGE_I2_IOEX, POWER_CHARGE_I2, HIGH);
      break;
  }
  Serial.print("set input current: ");
  Serial.println(nameOf(current));
}
